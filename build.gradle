plugins {
    id 'java'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.uebliche.mcmeta'
}

group = 'net.uebliche'
version = new Date().format('yyyy.MM.dd')

ext.baseCoordinates = "${project.name}-${project.version}"

def resolveGameVersions = {
    def override = System.getenv("MODRINTH_GAME_VERSIONS")
    if (override != null && !override.trim().isEmpty()) {
        return override.split(',').collect { it.trim() }.findAll { it }
    }
    def manifestUrl = mcmeta.manifestUrl
    try {
        def parsed = new groovy.json.JsonSlurper().parse(new URL(manifestUrl))
        def versions = parsed.versions
                .findAll { it.type == 'release' }
                .collect { it.id }
                .findAll { it }
        return versions.unique()
    } catch (Exception e) {
        logger.warn("Failed to resolve MC versions from mcmeta manifest: ${e.message}")
        return []
    }
}

mcmeta {
    minecraftVersion = project.findProperty("mcVersion")?.toString() ?: ""
    repositories {
        all()
    }
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20-R0.1-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }

    // Hier wird der Ausschluss der META-INF-Ordner für Ressourcen durchgeführt
    exclude 'META-INF/**'
}

jar {
    exclude 'META-INF/MANIFEST.MF'
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "YCMlX69E"
    version = version
    versionType = "release"
    uploadFile = jar
    gameVersions = resolveGameVersions()
    loaders = [
            "bukkit",
            "paper",
            "purpur",
            "spigot",
            "folia"
    ]
}
